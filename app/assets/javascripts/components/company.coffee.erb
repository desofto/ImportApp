@Company = React.createClass

  getInitialState: ->
    open: false
    operations: []
    loading: false

  handleToggle: (e) ->
    e.preventDefault()
    if @state.open
      @setState open: false
    else
      @setState loading: true
      $.ajax
        method: 'GET'
        url: "/companies/#{ @props.company.id }/operations"
        dataType: 'JSON'
        success: (data) =>
          @setState operations: data, open: true, loading: false

  render: ->
    React.DOM.div
      className: 'company'
      React.DOM.div
        className: 'row'
        React.DOM.div
          className: 'col-md-3'
          React.DOM.h4
            className: 'name ' + (if @state.open then 'open' else 'closed')
            onClick: @handleToggle
            @props.company.name
        React.DOM.div
          className: 'col-md-2'
          "Operations: #{ @props.company.operations }"
        React.DOM.div
          className: 'col-md-2'
          "Avg: #{ @props.company.average_amount }"
        React.DOM.div
          className: 'col-md-2'
          "Highest: #{ @props.company.highest_operation }"
        React.DOM.div
          className: 'col-md-2'
          "Accepted: #{ @props.company.accepted }"
        React.DOM.div
          className: 'col-md-1'
        React.DOM.a
          className: 'btn btn-default'
          href: "/companies/#{ @props.company.id }/operations.csv?filter=#{ @props.filter }"
          'CSV'
      if @state.loading
        React.DOM.div
          className: 'row'
          React.DOM.div
            className: 'col-md-12'
            React.DOM.img
              src: '<%= asset_path('loading-line.gif') %>'
      if @state.open
        React.createElement Operations, operations: @state.operations, filter: @props.filter
